# https://lakur.tech/2021/12/10/fix-slow-zsh-startup-nvm/
export NVM_LAZY=1

source ~/.zplug/init.zsh

zplug "plugins/git",     from:oh-my-zsh
zplug "plugins/tmux",    from:oh-my-zsh
zplug "plugins/z",       from:oh-my-zsh
zplug "plugins/gitfast", from:oh-my-zsh
zplug "plugins/common-aliases", from:oh-my-zsh
zplug "lukechilds/zsh-nvm"

zplug "zsh-users/zsh-syntax-highlighting", defer:2

zplug "themes/robbyrussell", from:oh-my-zsh, as:theme

# Install plugins if there are plugins that have not been installed
if ! zplug check --verbose; then
    printf "Install? [y/N]: "
    if read -q; then
        echo; zplug install
    fi
fi

# Then, source plugins and add commands to $PATH
zplug load

# enable history
export HISTFILE=~/.zsh_history
export HISTSIZE=10000
export SAVEHIST=10000

if [ -f ~/.iterm2_shell_integration.zsh ]; then
    source ~/.iterm2_shell_integration.zsh
fi

autoload -U compinit && compinit
zmodload -i zsh/complist

# keep zsh in emacs mode
bindkey -e

bindkey '^R' history-incremental-search-backward
bindkey "^P" up-line-or-search

# You may need to manually set your language environment
export LANG=en_US.UTF-8
export LC_ALL=en_US.UTF-8
export LC_CTYPE=en_US.UTF-8

# /opt/homebrew/bin is not loaded by default make sure it is always there
export PATH=/opt/homebrew/bin:$PATH
source $HOME/.zsh/path.zsh

if which rbenv > /dev/null; then eval "$(rbenv init -)"; fi

alias rssh='rsync --progress -avz -e "ssh"'

# make nvim the default vim
alias vim='nvim'
alias vi='nvim'

export EDITOR="nvim"

function current_epoch() {
    zmodload zsh/datetime
    echo $(( EPOCHSECONDS / 60 / 60 / 24 ))
}

REALLINK="$(readlink -f ~/.zshrc)"
DOTFILES_PATH="$(dirname $REALLINK)/../../../"

function update_last_updated_file() {
    echo "LAST_EPOCH=$(current_epoch)" >! "${HOME}/.dotfiles-epoch"
}

check_dotfiles() {
    if (( $(git status --porcelain=v1 2>/dev/null | wc -l) )); then
        echo "[dotfiles] uncommited changes, consider adding them and push upstream"
    fi

    git remote -v update > /dev/null 2>&1
    UPSTREAM=${1:-'@{u}'}
    LOCAL=$(git rev-parse @)
    REMOTE=$(git rev-parse "$UPSTREAM")
    BASE=$(git merge-base @ "$UPSTREAM")

    if [ $LOCAL = $REMOTE ]; then
        update_last_updated_file
    elif [ $LOCAL = $BASE ]; then
        echo "[dotfiles] please update your local head with: update_dotfiles"
    elif [ $REMOTE = $BASE ]; then
        echo "[dotfiles] unpushed changes, consider pushing upstream"
    else
        echo "[dotfiles] diverged, try merge local changes with remote head"
    fi
}

update_dotfiles() {
    echo "[dotfiles] pulling"
    (cd $DOTFILES_PATH && git pull)
}


function daily_dotfiles_check() {
    if ! source "${HOME}/.dotfiles-epoch" 2>/dev/null || [[ -z "$LAST_EPOCH" ]]; then
        update_last_updated_file
        LAST_EPOCH=$(current_epoch)
    fi

    epoch_target=1
    if (( ( $(current_epoch) - $LAST_EPOCH ) >= $epoch_target )); then
        (cd $DOTFILES_PATH && check_dotfiles)
    fi
}

daily_dotfiles_check
